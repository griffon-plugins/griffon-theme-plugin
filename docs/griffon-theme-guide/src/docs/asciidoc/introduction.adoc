
[[_introduction]]
= Introduction

Themes may be switched at any time based on two conditions:

 * a valid value for `ThemeManager.currentTheme` is set
 * the application's Locale is updated

The following controller shows 4 actions showing how to trigger each one of
these conditions. The application assumes there are two themes named `red` and
`blue` and that there are locale aware versions of these themes for English and
Spanish

[source,groovy,linenumbers,options="nowrap"]
----
import griffon.core.artifact.GriffonController
import griffon.plugins.theme.ThemeManager
import org.kordamp.jipsy.annotations.ServiceProviderFor
import javax.inject.Inject

@ServiceProviderFor(GriffonController)
class SampleController {
    @Inject
    private ThemeManager themeManager

    void red(evt)  { themeManager.currentTheme = 'red' }
    void blue(evt) { themeManager.currentTheme = 'blue' }

    void spanish(evt) { application.localeAsString = 'es' }
    void english(evt) { application.localeAsString = 'en' }
}
----

Classes that should participate in theme injection must be anotated with
`{link_api_theme_aware}` and have their properties anotated with `@InjectedResource`,
for example

[source,groovy,linenumbers,options="nowrap"]
----
import griffon.core.artifact.GriffonModel
import griffon.transform.beans.Observable
import griffon.core.resources.InjectedResource
import org.kordamp.jipsy.annotations.ServiceProviderFor
import java.awt.Color

@griffon.plugins.theme.ThemeAware
@ServiceProviderFor(GriffonModel)
class SampleModel {
    @Observable @InjectedResource Color color
    @Observable @InjectedResource String message
}
----

The resource injection mechanism relies on application events in order to handle
injections on instances. All griffon artifacts trigger an event upon creation
(`NewInstance`) and destruction (`DestroyInstance`). Non griffon artifact
instances can still participate in resource injection as long their classes
are bound within a `Module`.

Marking bean properties as observable makes it easier for the application to
update itself when a theme change occurs. For example, a View may use the
`color` and `message` model properties in this way

[source,groovy,linenumbers,options="nowrap"]
----
import griffon.core.artifact.GriffonView
import org.kordamp.jipsy.annotations.ServiceProviderFor
import javax.swing.SwingConstants

@ServiceProviderFor(GriffonView)
class SampleView {
    FactoryBuilderSupport builder
    SampleModel model

    void initUI() {
        builder.with {
            application(title: 'Themes',
                preferredSize: [320, 240], pack: true,
                locationByPlatform: true) {
                borderLayout()
                label(text: bind { model.message },
                      foreground: bind { model.color },
                      constraints: CENTER)
                  panel(constraints: WEST) {
                      gridLayout(cols: 1, rows: 4)
                      button(redAction)
                      button(blueAction)
                      button(spanishAction)
                      button(englishAction)
                  }
            }
        }
    }
}
----

It's worth noting that if a resource cannot be resolved by a theme then the
default application resources will be searched until the resource can be
resolved or a `NoSuchResourceException` is thrown.

== Configuration

Theme files look exactly the same as resources files. For the example shown
above, the application expects the following files to exist

 * griffon-app/i18n/red.properties
 * griffon-app/i18n/red_es.properties
 * griffon-app/i18n/blue.properties
 * griffon-app/i18n/blue_es.properties

These themes must be registered using a Module, for example

:subproject-base: {rootdir}/subprojects/griffon-theme-
:test-source: /src/test/java/
:package-base: org/codehaus/griffon/runtime/theme

[source,java,options="nowrap"]
----
include::{subproject-base}core{test-source}{package-base}/ThemeTest.java[tags=bindings,indent=0]
----

Griffon version: {griffon-version}


